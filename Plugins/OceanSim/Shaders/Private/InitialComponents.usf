#include "/OceanShaders/Private/Common.ush"

Texture2D<float4> NoiseTexture;
RWTexture2D<float4> ComponentTexture;

float Phillips(float2 K)
{	
    float L = WindSpeed * WindSpeed / Gravity;
	float l = L / 1000;

	float2 k = normalize(K);
    float2 w = normalize(WindDirection);
	float k2 = K.x * K.x + K.y * K.y;

    float kw = dot(k, w);

    return Amplitude * exp(-1 / (k2 * L * L)) * pow(kw, 6) * exp(-k2 * l * l) / (k2 * k2);
}

[numthreads(THREADGROUPSIZE_X, THREADGROUPSIZE_Y, 1)]
void main( uint3 id : SV_DispatchThreadID )
{
	float4 InputNoise = NoiseTexture[id.xy];

	float2 K = CalculateK(id.xy);
    float Magnitude = sqrt(Phillips(K)) / sqrt(2);
    float Magnitude2 = sqrt(Phillips(-K)) / sqrt(2);
    
    if (isnan(Magnitude))
    {
        Magnitude = 0;
    }
    
    if (isnan(Magnitude2))
    {
        Magnitude2 = 0;
    }
	
    ComponentTexture[id.xy] = float4(Magnitude * InputNoise.xy, Magnitude2 * InputNoise.zw);
}